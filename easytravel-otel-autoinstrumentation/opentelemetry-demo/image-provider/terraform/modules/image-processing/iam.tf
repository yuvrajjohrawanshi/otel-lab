resource "aws_iam_role" "this" {
  name = local.name_prefix

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Principal = {
          Service = "lambda.amazonaws.com"
        }
      },
    ]
  })
}
resource "aws_iam_role_policy" "access_s3_bucket" {
  name = "${local.name_prefix}-access-s3-bucket"
  role = aws_iam_role.this.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect : "Allow"
        Resource = [for bucket in aws_s3_bucket.products : "${bucket.arn}/*"]
        Action = [
          "s3:GetObject",
          "s3:PutObject",
          "s3:PutObjectTagging"
        ]
      },
      {
        Effect : "Allow"
        Resource = [for bucket in aws_s3_bucket.products : bucket.arn]
        Action = [
          "s3:ListBucket"
        ]
      }
    ]
  })
}

resource "aws_iam_role_policy" "lambda_basic_execution" {
  name = "${local.name_prefix}-lambda-basic-execution"
  role = aws_iam_role.this.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect : "Allow"
        Resource = "arn:aws:logs:${var.aws_region}:${var.aws_account_id}:*"
        Action   = "logs:CreateLogGroup"
      },
      {
        Effect : "Allow"
        Resource = "arn:aws:logs:${var.aws_region}:${var.aws_account_id}:log-group:/aws/lambda/${aws_lambda_function.this.function_name}:*"
        Action = [
          "logs:CreateLogStream",
          "logs:PutLogEvents"
        ]
      }
    ]
  })
}

resource "aws_iam_role_policy" "access_vpc_execution_role" {
  name = "${local.name_prefix}-access-vpc-execution-role"
  role = aws_iam_role.this.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect : "Allow"
        Resource = "*"
        Action = [
          "ec2:CreateNetworkInterface",
          "ec2:DeleteNetworkInterface",
          "ec2:DescribeNetworkInterfaces"
        ]
      }
    ]
  })
}

resource "aws_iam_role_policy" "dynamodb_read_only_access" {
  name = "${local.name_prefix}-dynamodb-read-only"
  role = aws_iam_role.this.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect : "Allow"
        Resource = local.image_provider_failure_enabled ? "${aws_dynamodb_table.this.arn}/*" : aws_dynamodb_table.this.arn
        Action = [
          "dynamodb:GetItem"
        ]
      }
    ]
  })
}
